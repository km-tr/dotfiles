# environment variables
export EDITOR=vim

export HISTSIZE=10000
export SAVEHIST=10000

export XDG_CONFIG_HOME=$HOME/.config

export ANDROID_HOME=$HOME/Library/Android/sdk
path+=($ANDROID_HOME/emulator)
path+=($ANDROID_HOME/platform-tools)

export GOPATH=$HOME/go
path+=($GOPATH/bin)

export BUNPATH=$HOME/.bun
path+=($BUNPATH/bin)

path+=$HOME/.local/bin

path+=/opt/homebrew/opt/protobuf@3/bin
path+=/opt/homebrew/opt/postgresql@15/bin

path+=$HOME/.lmstudio/bin

# eval
eval "$(/opt/homebrew/bin/brew shellenv)"

[[ "$COMPOSER_NO_INTERACTION" != "1" ]] && eval "$(sheldon source)"

eval "$(/opt/homebrew/bin/mise activate zsh)"

eval "$(zoxide init zsh)"

source <(fzf --zsh)

source /opt/homebrew/share/zsh-abbr/zsh-abbr.zsh

eval "$(wtp shell-init zsh)"

# aliases
alias tis='tig status'

alias dc='docker compose'

alias gcmf='git commit -m "feat:'
alias gcmfi='git commit -m "fix:'
alias gcmd='git commit -m "docs:'
alias gcms='git commit -m "style:'
alias gcmr='git commit -m "refactor:'
alias gcmp='git commit -m "perf:'
alias gcmt='git commit -m "test:'
alias gcmb='git commit -m "build:'
alias gcmci='git commit -m "ci:'
alias gcmc='git commit -m "chore:'
alias gcmrv='git commit -m "revert:'

alias cm='chezmoi'

alias brwe="brew"
alias bbd="brew bundle dump --global --force"

alias lg="lazygit"

# The next line updates PATH for the Google Cloud SDK.
if [ -f "$HOME/google-cloud-sdk/path.zsh.inc" ]; then . "$HOME/google-cloud-sdk/path.zsh.inc"; fi

# The next line enables shell command completion for gcloud.
if [ -f "$HOME/google-cloud-sdk/completion.zsh.inc" ]; then . "$HOME/google-cloud-sdk/completion.zsh.inc"; fi


gwcd() {
    cd "$(git worktree list | fzf | awk '{print $1}')"
}

y() {
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
	command yazi "$@" --cwd-file="$tmp"
	IFS= read -r -d '' cwd < "$tmp"
	[ "$cwd" != "$PWD" ] && [ -d "$cwd" ] && builtin cd -- "$cwd"
	rm -f -- "$tmp"
}

wi() {
  # set -euo pipefail

  local base="origin/master"
  local launch=""   # codex | claude | ""

  local -a args
  args=()

  while (( $# )); do
    case "$1" in
      --base)
        base="${2:?missing value for --base}"; shift 2 ;;
      --codex)
        if [[ -n "$launch" && "$launch" != "codex" ]]; then
          echo "ERROR: use only one of --codex or --claude" >&2
          return 1
        fi
        launch="codex"; shift ;;
      --claude)
        if [[ -n "$launch" && "$launch" != "claude" ]]; then
          echo "ERROR: use only one of --codex or --claude" >&2
          return 1
        fi
        launch="claude"; shift ;;
      --)
        shift; args+=("$@"); break ;;
      -*)
        echo "Unknown flag: $1" >&2; return 1 ;;
      *)
        args+=("$1"); shift ;;
    esac
  done

  local raw="${(j: :)args}"
  if [[ -z "${raw// }" ]]; then
    echo 'usage: wi "<title #111>" | wi "111" [--base origin/master] [--codex|--claude]' >&2
    return 1
  fi

  # issue番号を抽出（LLMに任せない）
  local issue
  issue="$(echo "$raw" | grep -Eo '#?[0-9]+' | head -n1 | tr -d '#')"
  if [[ -z "$issue" ]]; then
    echo 'ERROR: issue number not found. Include "#111" or "111" in the input.' >&2
    return 1
  fi

  # title: rawから最初の issue token を消してトリム
  local title
  title="$(echo "$raw" | sed -E "s/(^|[^0-9])#?$issue([^0-9]|$)/ /" | sed -E 's/[[:space:]]+/ /g; s/^ //; s/ $//')"

  # titleが空ならghから補完
  if [[ -z "$title" ]]; then
    if command -v gh >/dev/null 2>&1; then
      title="$(gh issue view "$issue" --json title -q .title 2>/dev/null || true)"
    fi
  fi
  if [[ -z "$title" ]]; then
    echo "ERROR: title is empty. Provide a title or ensure 'gh issue view $issue' works." >&2
    return 1
  fi

  local rules_file="$HOME/docs/branch.md"
  [[ -f "$rules_file" ]] || { echo "ERROR: rules file not found: $rules_file" >&2; return 1; }
  command -v llm >/dev/null 2>&1 || { echo "ERROR: 'llm' not found. Install with: pipx install llm" >&2; return 1; }

  local rules out slug
  rules="$(cat "$rules_file")"

  out="$(
    printf "%s\n\nTASK:\nReturn ONLY a git branch slug in kebab-case and append -%s.\nNo extra text.\n\nINPUT:\ntitle: %s\nissue: %s\n" \
      "$rules" "$issue" "$title" "$issue" \
    | llm -m llama3.2:1b -s "Output only: <kebab-case>"
  )"

  slug="$(echo "$out" | tr -d '\r' | head -n1 | sed -E 's/^[[:space:]]+|[[:space:]]+$//g')"

  if [[ -z "$slug" || "$slug" =~ [[:space:]] ]]; then
    echo "ERROR: invalid slug: $slug" >&2
    return 1
  fi
  if [[ ! "$slug" =~ ^[a-z0-9][a-z0-9-]*-[0-9]+$ ]]; then
    echo "ERROR: unexpected slug format: $slug" >&2
    return 1
  fi

  local branch="${slug}"

  echo "input : $raw"
  echo "title : $title"
  echo "issue : $issue"
  echo "base  : $base"
  echo "branch: $branch"

  # wtp add は [<commit>] を第2引数で受ける
  # wtp add -b "$branch" "$base"

  # 遷移
  # wtp cd "$branch"

  # 起動（どちらか一方のみ）
  case "$launch" in
    codex)
      command -v codex >/dev/null 2>&1 || { echo "ERROR: codex not found in PATH" >&2; return 1; }
      codex
      ;;
    claude)
      command -v claude >/dev/null 2>&1 || { echo "ERROR: claude not found in PATH" >&2; return 1; }
      claude
      ;;
    "")
      ;;
  esac
}